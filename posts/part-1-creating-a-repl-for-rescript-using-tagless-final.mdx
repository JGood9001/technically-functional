---
title: "Part 1: Creating a REPL for Rescript using Tagless Final"
slug: "part-1-creating-a-repl-for-rescript-using-tagless-final"
description: "Creating a REPL for Rescript using Tagless Final"
date: "May 14, 2023"
---

The goal of this series of blog posts is to explore a small problem where Tagless Final can be applied. There's some initial code I wrote while hacking together a quick REPL for ReScript that I wanted to use, and in preparation for actually creating a proper library that could potentially be reused amongst the ReScript community, I figured a proper refactor is necessary.

If you're unfamiliar with Tagless Final, here's the [blog post](https://oleksandrmanzyuk.wordpress.com/2014/06/18/from-object-algebras-to-finally-tagless-interpreters-2/) I read that introduced me to the approach; and a link to a [ReScript Playground snippet](https://rescript-lang.org/try?version=v10.1.2&code=PTAEEEBtNAzAnA9gW1AFwBYFNQGcsDGaAlogHYD6siArvAHTIyzGQ7FnoCGA5m7riocukAFAhQAdy644iaIkkceoEeRVLM6DMVkAjSIhUAHRLjQAucWAxo0x3BZDysAa1xcyAE3jJPALwBPGld6SUR4L2N4LAF6AhRgACYABgBGABZgFIA2YDSADmAEFABaRD0AK0I0UpEeLD14GVK0RFKWMhFIQNbeflxSjjQseGisEfhBpOBRZEQvGjZ0QOMcAFEAD2MoFQBeUABvUVAASDRVnDRQUVO2a8hia4tQYdA9gD50E7P71S8vKAXtdPuh3l80KIAL6iUQXNagLAANxE71A6xRkAAFMMAJSwiQYGSuLDQKS0SBeADkSJwXBobQaZFGXBGgMwOAadlGcng2l0KzWVNkkmaxjWgMxNCwoj+yNRByxGJEWLIuNx4NAZFh80Wy2VkC2xjRx1AgquaPlYjNf0eIK1moNqvxNom-0Bis2ABpQIENaCnVasZsNQBqRGYrF+-Ew6ygAAq2ER2xiAlInCxaVA4axSWzoAAzOrXrIYuN8GQ2apZNQFJJZFjYFwCKwnqysIC9IE4MQptcCJAZLJdUtYrirHKs4qR8slUD0dtdlInhhzWCDlbi59but6FwAUr6HbM7ifbvj0l1TqFqO1wBZJZLg6m04cAc0LwbReQHg-H53N1kCWecQQhTVIVjGccAfQ1MSNE0TlfMh30-dE4O2P8zQA64gJgT0fT9R1IyDENQAAKgjFVo2hWE5Tzacb1nO95xgpdNFXeELQ3TEtw+W470YJYsQE480lPUARKeXMrzjSQsF7LwfXCMgqX7FBjFYHB2Noa4OVAKDQD0eSyA0MUJVeThjC4GJK2wfBcB9PQGW0Vk410FTdKTAgZHYTgAHkAGEuCYWU3SRKdESSLEDOEpYDSNK8gA) where I translated a few of the examples from that blog post into ReScript.

Upon initial review of the spaghetti code that made up the prototype REPL, I identified and delineated two separate interfaces:
  1. Command Line
  2. Domain Logic

Where the Command Line interface for the Production instance of the interface leverages the [Readline module](https://github.com/TheSpyder/rescript-nodejs/blob/main/src/Readline.res) from the NodeJs API, and a test instance may be provided in order to perform integration tests and verify the Production instance for the Domain Logic works appropriately in response to user input provided via the Command Line interface.

Of the two benefits of the Tagless Final approach, the second point made in the previous sentence is what I find to be an extremely compelling reason to use it.

src/interfaces/CommandLineIOAlg.res:
```res
type commandLineIO<'a> = CommandLineIO('a)

module type CommandLineIOAlg = {
  type t
  let make : () => commandLineIO<t>
  let prompt : commandLineIO<t> => string => (string =>  Promise.t<'a>) => Promise.t<'a>
  let on : commandLineIO<t> => string => (() => ()) => commandLineIO<t>
  let close : commandLineIO<t> => unit
 }
```

src/interfaces/DomainLogicAlg.res:
```res
type cont_or_close = Continue | Close

module type DomainLogicAlg = {
    let handleUserInput : string => Promise.t<cont_or_close>
    let cleanup : () => ()
}
```

src/repl.res:
```res
open CommandLineIOAlg
open DomainLogicAlg

let handle_cont_or_close = (cont_or_close, cont, close): Promise.t<cont_or_close> => {
    Promise.make((resolve, _reject) => {
        switch cont_or_close {
            | Continue => {
                cont()->Promise.then(_ => Promise.make((res, _rej) => res(. ())))->ignore // the Promise.then becomes necessary because the recursive function is async... now
                resolve(. cont_or_close)
            }
            | Close => {
                Js.log("See you Space Cowboy")
                close()
                resolve(. cont_or_close)
            }
        }
    })
}

let repl = async (module (CLIO : CommandLineIOAlg), module (DL : DomainLogicAlg)) => {
    let cliInterface = CLIO.on(CLIO.make(), "close", DL.cleanup)
    let close = () => CLIO.close(cliInterface)
    let prompt = async () => await CLIO.prompt(cliInterface, "\u03BB> ", DL.handleUserInput) 

    let rec run_loop: () => Promise.t<cont_or_close> = async () => {
        let cont_or_close = await prompt()
        await handle_cont_or_close(cont_or_close, run_loop, close)
    }

    await run_loop()
}
```

src/RunRepl.res
```res
open NodeJs
open CommandLineIOAlg
open DomainLogicAlg
open NewRepl

@module("fs") external unlinkSync: string => () = "unlinkSync"

// Here, the library author is the end user... so this is really just putting it all together
// so that package will work as advertised.

// ******************************************
// CommandLineIO instance
// ******************************************

module CommandLineIOAlg = {
    type t = Readline.Interface.t

    let make = () =>
    Readline.make(
        Readline.interfaceOptions(~input=Process.process->Process.stdin, ~output=Process.process->Process.stdout, ()),
    )->CommandLineIO

    let prompt = (CommandLineIO(rl), query, cb) =>
    Promise.make((resolve, _reject) => rl->Readline.Interface.question(query, x => resolve(. x)))
    ->Promise.then(user_input => cb(user_input))

    let on = (CommandLineIO(rl), event, cb) =>
    rl->Readline.Interface.on(Event.fromString(event), cb)->CommandLineIO

    let close = (CommandLineIO(rl)) => rl->Readline.Interface.close
}

// ******************************************
// Rescript REPL Specific Domain Logic instance
// ******************************************

module DomainLogicAlg = {
    let handleUserInput = (s: string) => {
        Promise.make((resolve, _reject) => resolve(. Close))
    }

    let cleanup = () => {
        unlinkSync("./src/RescriptRepl.res")
        unlinkSync("./src/RescriptRepl.bs.js")
        ()
    }
}

let run_repl = () => repl(module (CommandLineIOAlg), module (DomainLogicAlg))
```

With this in place, the basic REPL flow is setup.
In the next part, we'll write parser combinators to parse user input into a REPLCommand
in order to determine what actions should be taken.